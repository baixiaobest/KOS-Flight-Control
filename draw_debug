run once subscription_data.
run once publish_subscribe.

local function create {
	local pub_sub is PublishSubscribe().
	local vector_debug_name_value_map is lexicon().

	function vector_debug_callback {
		parameter debug_msg.

		local name is debug_msg:name.
		local from_vec is debug_msg:from_vec.
		local to_vec is debug_msg:to_vec.
		local color is debug_msg:color.

		if vector_debug_name_value_map:haskey(name) {
			set vector_debug_name_value_map[name] to lexicon(
				"from_vec", from_vec, "to_vec", to_vec).
		}
		else {
			if color = 0 {
				set color to rgb(random(), random(), random()).
			}

			vector_debug_name_value_map:add(
				name, 
				lexicon("from_vec", from_vec, "to_vec", to_vec)).
			
			vecdraw(
				{return vector_debug_name_value_map[name]:from_vec.},
				{return vector_debug_name_value_map[name]:to_vec.},
				color,
				name,
				1.0,
				true).
		}
	}

	function init {
		pub_sub:subscribe("/debug/vector", vector_debug_callback@).
	}

	function run {

	}

	return lexicon(
		"run", run@,
		"init", init@).
}

local draw_debug_obj is create().

function DrawDebug {
	return draw_debug_obj.
}

function draw_debug_vector {
	parameter name.
	parameter from_vec.
	parameter to_vec.
	parameter color is 0.

	PublishSubscribe():publish(
		"/debug/vector", 
		lexicon("name", name,
				"from_vec", from_vec,
				"to_vec", to_vec,
				"color", color)).
}